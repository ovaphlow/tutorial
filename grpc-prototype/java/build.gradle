plugins {
    id 'application'
    id 'com.google.protobuf' version '0.8.8'
    id 'idea'
}

repositories {
    jcenter()
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

def grpcVersion = '1.23.0'
def protobufVersion = '3.9.0'
def protocVersion = protobufVersion

dependencies {
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    compileOnly "javax.annotation:javax.annotation-api:1.2"

    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"

    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"

    implementation "com.zaxxer:HikariCP-java7:2.4.13"
    implementation "org.postgresql:postgresql:42.2.7"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

startScripts.enabled = false

task gRpcServer(type: CreateStartScripts) {
    mainClassName = 'ovaphlow.demo.grpc.GRpcServer'
    applicationName = 'gRpc-Server'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = startScripts.classpath
}

applicationDistribution.into('bin') {
    from(gRpcServer)
    fileMode = 0755
}
